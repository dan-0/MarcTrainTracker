/*
 * Copyright (c) 2018 IdleOffice Inc.
 *
 * build.gradle is part of MarcTrainTracker.
 *
 * MarcTrainTracker is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * MarcTrainTracker is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Foobar.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.idleoffice.marctrain"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 9
        versionName "1.9"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary= true
        manifestPlaceholders = [enableCrashlytics: "false"]
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [enableCrashlytics: "true"]
        }

        all {
            buildConfigField "String", "POLL_URL", "\"https://d2jr231zyecel4.cloudfront.net/\""
            buildConfigField "long", "ALERT_POLL_INTERVAL", "60"
            buildConfigField "long", "ALERT_POLL_RETRY_INTERVAL", "10"
            buildConfigField "long", "STATUS_POLL_INTERVAL", "60"
            buildConfigField "long", "STATUS_POLL_RETRY_INTERVAL", "10"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

configurations.all { config ->
    if (config.name.contains('UnitTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0"

    implementation "androidx.appcompat:appcompat:$androidXVersion"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "com.google.android.material:material:1.0.0"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.2.0"
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    testImplementation "androidx.arch.core:core-testing:2.0.0"

    androidTestImplementation "androidx.test.ext:junit:$androidXTestVersion"
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"

    // Java Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$archVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$archVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$archVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$archVersion"

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.7'
    implementation 'com.google.firebase:firebase-core:16.0.6'

    // DI
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"
    testImplementation "org.koin:koin-test:$koinVersion"

    // Timber for logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Moshi
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
}

apply plugin: 'com.google.gms.google-services'