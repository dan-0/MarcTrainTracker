/*
 * Copyright (c) 2019 IdleOffice Inc.
 *
 * build.gradle is part of MarcTrainTracker.
 *
 * MarcTrainTracker is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * MarcTrainTracker is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this software.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.idleoffice.marctrain"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 23
        versionName "1.23"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [enableCrashlytics: "false"]
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            manifestPlaceholders = [enableCrashlytics: "true"]
        }

        all {
            buildConfigField "String", "POLL_URL", "\"https://d2jr231zyecel4.cloudfront.net/\""
        }
    }
    flavorDimensions "dev"
    productFlavors {
        mock {

        }
        live {
            manifestPlaceholders = [enableCrashlytics: "true"]
        }
    }

    buildFeatures {
        viewBinding = true
    }

    testOptions {
        animationsDisabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared/java"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/testShared/java"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'

    // Unit Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
    testImplementation 'androidx.arch.core:core-testing:2.0.1'

    // UI testing
    def espressoVersion = "3.2.0"
    def androidXTestVersion = "1.2.0"
    implementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "androidx.test:orchestrator:$androidXTestVersion"
    androidTestImplementation "androidx.test:rules:$androidXTestVersion"

    // ViewModel and LiveData
    def archVersion = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$archVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$archVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$archVersion"

    // Crashlytics
    implementation 'com.google.firebase:firebase-analytics:17.4.4'
    implementation 'com.google.firebase:firebase-crashlytics:17.1.1'

    // DI
    def koinVersion = "1.0.2"
    implementation "org.koin:koin-android:1.0.2"
    implementation 'org.koin:koin-androidx-viewmodel:1.0.2'
    testImplementation "org.koin:koin-test:$koinVersion"

    // Timber for logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Moshi
    def moshiVersion = "1.8.0"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    // Retrofit
    def retrofitVersion = "2.8.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.4"

    // Live event library
    implementation "com.github.hadilq.liveevent:liveevent:1.2.0"

    // Consent library
    implementation 'com.idleoffice:idleconsent:0.2.4-alpha'

    // KTX
    implementation "androidx.activity:activity-ktx:1.1.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    // Time
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'

    // Coroutines
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.6"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.6"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.6"

    // Jsoup
    implementation 'org.jsoup:jsoup:1.11.1'


    // Kotlin
    def nav_version = "2.3.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
}